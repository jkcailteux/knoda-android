import java.util.regex.Pattern

buildscript {
    repositories {
        mavenCentral()

    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.+'
        classpath 'org.robolectric:robolectric-gradle-plugin:0.12.+'
    }
}
apply plugin: 'android'
apply plugin: 'robolectric'


configurations {
    provided
}

repositories {
    maven {
        url 'https://github.com/knodafuture/knoda-volley/raw/mvn-repo'
    }
    mavenCentral()
}

android {
    compileSdkVersion 19
    buildToolsVersion '19.1.0'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 19
        renderscriptTargetApi 19
        renderscriptSupportMode true
        resConfigs "en"
        resConfigs "hdpi", "xhdpi", "xxhdpi"
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
    }

    sourceSets {
        androidTest{
            setRoot('src/test')
        }
    }
}

robolectric {
    // configure the set of classes for JUnit tests
    include '**/*Test.class'
    exclude '**/espresso/**/*.class'

    // configure max heap size of the test JVM
    maxHeapSize = '2048m'

    // configure whether failing tests should fail the build
    ignoreFailures true

    // use afterTest to listen to the test execution results
//    afterTest { descriptor, result ->
//        println "Executing test for {$descriptor.name} with result: ${result.resultType}"
//    }
}

dependencies {
    compile 'com.squareup:otto:1.3.4'
    compile 'com.google.android.gms:play-services:4.2.42'
    compile 'com.android.support:support-v13:20.0.+'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.knoda:volley:1.0.0'
    compile 'com.squareup.dagger:dagger:1.2.0'
    compile 'com.squareup.dagger:dagger-compiler:1.2.0'
    compile 'com.jakewharton:butterknife:4.0.1'
    compile 'org.apache.httpcomponents:httpcore:4.3'
    compile 'org.apache.httpcomponents:httpmime:4.3'
    compile 'joda-time:joda-time:2.3'
    compile 'commons-io:commons-io:2.4'
    compile 'org.apache.commons:commons-lang3:3.2.1'
    compile 'org.twitter4j:twitter4j-core:4.0.1'
    compile project(':Libraries:CropImage')
    compile project(':Libraries:PullToRefresh')
    compile files('lib/FlurryAnalytics_3.3.4.jar')
    compile files('lib/tapjoyconnectlibrary.jar')
    compile files('lib/AppRate_0.5.jar')
    compile project(':Libraries:facebook')

    androidTestCompile 'junit:junit:4.10'
    androidTestCompile 'org.robolectric:robolectric:2.3'
}

task incrementVersionCode << {
    println(":incrementVersionCode - Incrementing Version Code...")
    def manifestFile = file("src/main/AndroidManifest.xml")
    def patternVersionCode = Pattern.compile("versionCode=\"(\\d+)\"")
    def manifestText = manifestFile.getText()
    def matcherVersionCode = patternVersionCode.matcher(manifestText)
    matcherVersionCode.find()
    def mVersionCode = Integer.parseInt(matcherVersionCode.group(1))
    def mNextVersionCode = mVersionCode + 1
    def manifestContent = matcherVersionCode.replaceAll("versionCode=\"" + mNextVersionCode + "\"")
    println(":incrementVersionCode - current versionCode=" + mVersionCode);
    println(":incrementVersionCode - next versionCode=" + mNextVersionCode);
    manifestFile.write(manifestContent)
}


task verifyRelease << {
    println(":verifyProd - Ensure that NetworkingManager is pointed at production")
    def networkingManagerFile = file("src/main/java/managers/NetworkingManager.java")
    def networkingText = networkingManagerFile.getText()
    def testOccurrences = networkingText.indexOf("captaincold.knoda.com");
    def prodOccurrences = networkingText.indexOf("api.knoda.com");
    if (testOccurrences >= 0 || prodOccurrences < 0) {
        throw new GradleException("Build is not pointed at production.  Failing.")
    }
}